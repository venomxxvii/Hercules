// Settings :
//	-	Only required to edit the ShopSetting() Function
//			Notes : You may also add / remove Menu ( If any ) 
//	-	Shop Currency can be either ItemID or Variable Name, but must write within Quotation Marks ( "" )
//			Ex. of Variable.	->	Zeny , #CASHPOINTS , #KAFRAPOINTS , CustomVariable , #CustomVariable
//	-	ERROR Message are used to show Invalid Settings in your NPC.


// Leave this alone...
-	shop	Emistry_Shop	FAKE_NPC,512:100


prt_in,35,115,4	script	Warcraft Shop	4_M_EINMAN,{
function ShopSettings;
function ValidateCost;
function CurrencyInfo;
function ClearData;
function ValueConvert;
function ErrorNotice;

mes "[ ^FF0000Dawn^000000. ]";
mes "VPS Shop from the Menu may purchase using ^FF0000Voting Proof Ticket^000000.";
mes "Credit Shop from the Menu may purchase using ^FF0000Credits^000000.";
mes "Castle Drop Shop from the Menu may purchase using ^FF0000Warcraft Coin^000000.";
next;
// Menu Selection
select("VPS Shop","Credits Shop","Castle Drop Shop");

ClearData();
ShopSettings( @menu );
npcshopitem "Emistry_Shop",512,100;
npcshopdelitem "Emistry_Shop",512;
for(set .@i,0; .@i < getarraysize( @ItemLists ); set .@i,.@i+1)
	npcshopadditem "Emistry_Shop",@ItemLists[.@i],@ItemCost[.@i];
mes "Okay...wait awhile";
mes "^00FF00____________________________^000000";
CurrencyInfo( @Currency$ );
mes "^00FF00____________________________^000000";
callshop "Emistry_Shop",1;
npcshopattach "Emistry_Shop";
end;


function	ShopSettings	{
	switch( getarg(0) ){
		case 1:
			// Currency [ Item ID / Variable Name ]
			set @Currency$,"7227";
			// Item ID Lists
			setarray @ItemLists[0],7776,20010,20006,20007,20008,20009,20046,20047,20048,20049,2766,2767,2768,2769,2770,2771,20031,20035,20032,20036,20037,20034,20033,20066,20067,30014,30142,30143,30144,30145,30147,30018,30024,30026,30031,30037,30038,30039,30040,30041,30049,30051,30052,30054,30055,30056,30058,30061; //41
			// Item Price
			setarray @ItemCost[0],50,400,400,400,400,400,400,400,400,400,250,250,250,250,250,250,200,200,450,500,500,450,450,650,450,850,850,850,850,850,850,400,400,400,400,400,400,400,400,400,400,400,400,400,400,400,400,650; //41
			break;
		case 2:
			// Currency [ Item ID / Variable Name ]
			set @Currency$,"20030";
			// Item ID Lists
			setarray @ItemLists[0],12075,12080,12095,12090,12085,12100;
			// Item Price
			setarray @ItemCost[0],10,10,10,10,10,10;
			break;
//BG ITEMS			// Item ID Lists
//			setarray @ItemLists[0],2376,2377,2378,2379,2380,2381,2382,2540,2539,2538,2436,2437,2435;
//			// Item Price
//			setarray @ItemCost[0],20000,20000,20000,20000,20000,20000,20000,250,250,250,250,250,250;
//			break;
		case 3:
			// Currency [ Item ID / Variable Name ]
			set @Currency$,"674";
			// Item ID Lists
			setarray @ItemLists[0],7086,7077,7091,7090,7078,7092,7089,7079,7088,7837,7076,7073,7074,7075,7080,7081,7082,7083,7084,7085,7087; //21 CDROPS
			// Item Price
			setarray @ItemCost[0],300,200,200,200,200,200,200,200,200,200,200,200,200,200,200,200,200,200,200,200,200;
			break;
	}
if( @Currency$ == "" )
	ErrorNotice( "Invalid Currency Setting in Menu "+@menu+" ." );
if( getarraysize( @ItemCost ) != getarraysize( @ItemLists ) || getarraysize( @ItemLists ) != getarraysize( @ItemCost ) )
	ErrorNotice( "Missing or Extra Value of Item or Cost Settings in Menu "+@menu+" ." );
return;
}

function	ErrorNotice	{
	mes "^FF0000ERROR^000000 - "+getarg(0);
	mes "^00FF00____________________________^000000";
	mes "Inform this Message to ^0000FFGame Staffs^000000 immediately !";
	close;
}

function	CurrencyInfo	{
	if( getitemname( atoi( getarg(0) ) ) != "null" ){
		mes "Item Currency : ^FF0000"+getitemname( atoi( getarg(0) ) )+"^000000";
		mes "Available Amount : ^0000FF"+ValueConvert( countitem( atoi( getarg(0) ) ) )+"^000000";
	}else if( getitemname( atoi( getarg(0) ) ) == "null" ){
		mes "Variable Currency : ^FF0000"+getarg(0)+"^000000";
		mes "Available Amount : ^0000FF"+ValueConvert( getd( getarg(0) ) )+"^000000";
	}
return;
}

function	ValidateCost	{
	if( getitemname( atoi( getarg(0) ) ) != "null" ){
		if( countitem( atoi( getarg(0) ) ) < getarg(1) ) return 1;
	}else{
		if( getd( getarg(0) ) < getarg(1) ) return 1;
	}
return 0;
}

function ClearData	{
	set @Currency$,"";
	set @TotalCost,0;
	deletearray @bought_nameid[0],getarraysize( @bought_nameid );
	deletearray @bought_quantity[0],getarraysize( @bought_quantity );
	deletearray @ItemLists[0],getarraysize( @ItemLists );
	deletearray @ItemCost[0],getarraysize( @ItemCost );
return;
}

function	ValueConvert	{
	set .@num, atoi(""+getarg(0));
	if ( .@num == 0 || .@num >= 2147483647 ) return getarg(0);
	set .@l, getstrlen(""+.@num);
	for ( set .@i,0; .@i < .@l; set .@i, .@i + 1 ) {
		set .@num$, .@num % pow(10,.@i+1) / pow(10,.@i) + .@num$;
			if ( (.@i+1) % 3 == 0 && .@i+1 != .@l ) set .@num$, ","+ .@num$;
	}
	return .@num$;
}

OnBuyItem:
	ShopSettings( @menu );
	for(set @i,0; @i < getarraysize( @bought_nameid ); set @i,@i+1)
		for(set @j,0; @j < getarraysize( @ItemLists ); set @j,@j+1)
			if( @ItemLists[@j] == @bought_nameid[@i] )
			set @TotalCost,@TotalCost + ( @ItemCost[@j] * @bought_quantity[@i] );
	for( set @i,0; @i < getarraysize( @bought_nameid ); set @i,@i+1 )
			mes "^FF0000 "+@bought_quantity[@i]+" ^0000FF"+getitemname( @bought_nameid[@i] )+"^000000";

	if( getitemname( atoi( @Currency$ ) ) != "null" )
		mes "Total Cost : "+ValueConvert( @TotalCost )+" ^0000FF "+getitemname( atoi( @Currency$ ) )+"^000000";
	else if( getitemname( atoi( @Currency$ ) ) == "null" ){
		mes "Total Cost : ^0000FF"+ValueConvert( @TotalCost )+" "+@Currency$+"^000000";
	}
	
	mes "^00FF00____________________________^000000";
	if( ValidateCost( @Currency$,@TotalCost ) ){
		if( getitemname( atoi( @Currency$ ) ) != "null" )
			mes "Insufficient ^0000FF"+getitemname( atoi( @Currency$ ) )+"^000000";
		else{
			mes "Insufficient ^0000FF"+@Currency$+"^000000";
		}
	}else{
		if( select( "^0000FFPurchase^000000:Cancel" ) == 1 ){
			if( getitemname( atoi( @Currency$ ) ) != "null" )
				delitem atoi( @Currency$ ),@TotalCost;
			else{
				set getd( @Currency$ ),getd( @Currency$ ) - @TotalCost;
			}
			for(set @i,0; @i < getarraysize( @bought_nameid ); set @i,@i+1)
				getitem @bought_nameid[@i],@bought_quantity[@i];
			message strcharinfo(0),"Purchased "+getarraysize( @bought_nameid )+" Items.";
			mes "Thank you for shopping.";
		}
	}
ClearData();
close;

}