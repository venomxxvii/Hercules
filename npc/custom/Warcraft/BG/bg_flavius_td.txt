// ==============================================================================
// BattleGround System - queue Flavius TD, by Kubix. (Hercules edition)
// ==============================================================================
-	script	Flavius_TD	-1,{
	end;
	
OnGuillameQuit:
	bg_leave;
	if( bg_get_data($@Guill_BG, 0) == 0 || bg_get_data($@Croix_BG, 0) == 0 )
		donpcevent "Flavius_TD::OnMatchEnd";	
	setquest 8506;
	
OnGuillameDie:
	if( $@FlaviusTD == 1 )
	{
		.Guillaume_Score --;
		donpcevent "Flavius_TD::OnValidateScore";
	}
	end;
	
OnCroixQuit:
	bg_leave;
	if( bg_get_data($@Guill_BG, 0) == 0 || bg_get_data($@Croix_BG, 0) == 0 )
		donpcevent "Flavius_TD::OnMatchEnd";
	setquest 8506;
	
OnCroixDie:
	if( $@FlaviusTD == 1 )
	{
		.Croix_Score --;
		donpcevent "Flavius_TD::OnValidateScore";
	}
	end;
	
	
OnStartBattleground:
	if( $@FlaviusTD )
		end;
	
	set $@FlaviusTD, 1;
	initnpctimer;
	set .Guillaume_Score, $@Flavius_Score;
	set .Croix_Score, $@Flavius_Score;
	sleep 2000;
	bg_warp $@Guill_BG,"bat_b03",328,150;
	bg_warp $@Croix_BG,"bat_b03",62,150;
	sleep 3000;
	bg_team_reveal $@Guill_BG;
	bg_team_reveal $@Croix_BG;
	donpcevent "#guitd_respawn::OnBGStart";
	donpcevent "#crotd_respawn::OnBGStart";
	bg_updatescore "bat_b03",.Guillaume_Score,.Croix_Score;
	end;

OnValidateScore:
	if( $@FlaviusTD != 1 )
		end;

	bg_updatescore "bat_b03",.Guillaume_Score,.Croix_Score;
	set .@Guillaume, bg_get_data($@Guill_BG, 0);
	set .@Croix, bg_get_data($@Croix_BG, 0);

	if( .Croix_Score <= 0 )
	{ // Guillaume Won
		bg_reward $@Guill_BG,$@BG_RewardID,$@BG_WinnerAmount,0,8505,"BG_TDM",1,4,0;
		bg_reward $@Croix_BG,$@BG_RewardID,$@BG_LooserAmount,0,8505,"BG_TDM",1,4,0;
		mapannounce "bat_b03","The Guillaume army has won the Battle of Flavius TD!",1,0x0000FF;
	}
	else if( .Guillaume_Score <= 0 )
	{ // Croix Won
		bg_reward $@Guill_BG,$@BG_RewardID,$@BG_LooserAmount,0,8505,"BG_TDM",1,4,0;
		bg_reward $@Croix_BG,$@BG_RewardID,$@BG_WinnerAmount,0,8505,"BG_TDM",1,4,0;
		mapannounce "bat_b03","The Croix army has won the Battle of Flavius TD!",1,0xFF0000;
	}
	else if( .@Croix == 0 )
	{ // All Croix Players quit
		bg_reward $@Guill_BG,$@BG_RewardID,$@BG_WinnerAmount,0,8505,"BG_TDM",1,4,0;
		mapannounce "bat_b03","The Guillaume army has won the Battle of Flavius TD!",1,0x0000FF;
	}
	else if( .@Guillaume == 0 )
	{ // All Guillaume Players quit
		bg_reward $@Croix_BG,$@BG_RewardID,$@BG_WinnerAmount,0,8505,"BG_TDM",1,4,0;
		mapannounce "bat_b03","The Croix army has won the Battle of Flavius TD!",1,0xFF0000;
	}
	else end;
	donpcevent "Flavius_TD::OnMatchEnd";
	end;

OnTimer600000:
	mapannounce "bat_b03","The Battle will ends in 5 minutes!!",1,0x808000;
	end;

OnTimer840000:
	mapannounce "bat_b03","The Battle will ends in 1 minute!!",1,0x808000;
	end;

OnTimer900000:
	if( .Guillaume_Score > .Croix_Score )
	{ // Guillaume Won
		bg_reward $@Guill_BG,$@BG_RewardID,$@BG_WinnerAmount,0,8505,"BG_TDM",1,4,0;
		bg_reward $@Croix_BG,$@BG_RewardID,$@BG_LooserAmount,0,8505,"BG_TDM",1,4,0;
		mapannounce "bat_b03","The Guillaume army has won the Battle of Flavius TD!",1,0x0000FF;
	}
	else if( .Guillaume_Score < .Croix_Score )
	{ // Croix Won
		bg_reward $@Guill_BG,$@BG_RewardID,$@BG_LooserAmount,0,8505,"BG_TDM",1,4,0;
		bg_reward $@Croix_BG,$@BG_RewardID,$@BG_WinnerAmount,0,8505,"BG_TDM",1,4,0;
		mapannounce "bat_b03","The Croix army has won the Battle of Flavius TD!",1,0xFF0000;
	}
	else
	{
		bg_reward $@Guill_BG,$@BG_RewardID,$@BG_TieAmount,0,8505,"BG_TDM",1,4,0;
		bg_reward $@Croix_BG,$@BG_RewardID,$@BG_TieAmount,0,8505,"BG_TDM",1,4,0;
		mapannounce "bat_b03","The battle is over. This is a Tie...!",1,0x808000;
	}
	donpcevent "Flavius_TD::OnMatchEnd";
	end;

OnMatchEnd:
	set $@FlaviusTD, 2;
	$@BGStatus = 1;
	stopnpctimer;
	donpcevent "#guitd_respawn::OnBGStop";
	donpcevent "#crotd_respawn::OnBGStop";
	bg_warp $@Guill_BG,"bat_b03",390,10;
	bg_warp $@Croix_BG,"bat_b03",10,290;
	mapannounce "bat_b03","Battle of Flavius TD will stop in 5 seconds!",1,0x808000;
	initnpctimer;
	end;

OnTimer1000:
	if( $@FlaviusTD == 2 )
		mapannounce "bat_b03","Battle of Flavius TD will stop in 4 seconds!",1,0x808000;
	end;

OnTimer2000:
	if( $@FlaviusTD != 2 )
		end;
OnReset:
	stopnpctimer;
	set .Guillaume_Score, $@Flavius_Score;
	set .Croix_Score, $@Flavius_Score;
	bg_destroy $@Guill_BG;
	bg_destroy $@Croix_BG;
	set $@Guill_BG, 0;
	set $@Croix_BG, 0;
	sleep 1000;
	mapwarp "bat_b03","bat_room",155,150;
	sleep 1000;
	maprespawnguildid "bat_b03",0,3; // Just in case someone else
	bg_updatescore "bat_b03",$@Flavius_Score,$@Flavius_Score;
	set $@FlaviusTD, 0;
	donpcevent $@RegNPC$;
	end;
}

// Battleground Therapist
// *********************************************************************

bat_b03,390,13,5	script	Therapist in battle#td1	4_F_SISTER,{
	switch( $@FlaviusTD )
	{
	case 2:
		bg_leave;
		warp "bat_room",155,150;
		break;
	case 1:
		mes "[Therapist in battle]";
		mes "Just close your eyes, and take a deep breathe.";
		mes "You can be free from pain.";
		specialeffect2 312;
		percentheal 100,100;
		repairall;
		close2;
		openstorage;
		break;
	}
	end;
}

bat_b03,10,293,5	script	Therapist in battle#td2	4_F_SISTER,{
	switch( $@FlaviusTD )
	{
	case 2:
		bg_leave;
		warp "bat_room",155,150;
		break;
	case 1:
		mes "[Therapist in battle]";
		mes "Just close your eyes, and take a deep breathe.";
		mes "You can be free from pain.";
		specialeffect2 312;
		percentheal 100,100;
		repairall;
		close2;
		openstorage;
		break;
	}
	end;
}

// Battleground Respawn
// *********************************************************************

bat_b03,390,10,0	script	#guitd_respawn	HIDDEN_WARP_NPC,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_b03",382,2,397,17,100,100;
	areawarp "bat_b03",382,2,397,17,"bat_b03",306,138,327,161;
	initnpctimer;
	end;
}

bat_b03,10,290,0	script	#crotd_respawn	HIDDEN_WARP_NPC,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_b03",2,282,17,297,100,100;
	areawarp "bat_b03",2,282,17,297,"bat_b03",72,138,93,161;
	initnpctimer;
	end;
}

// Flags
// *********************************************************************

-	script	Guillaume camp#bat	1_FLAG_LION,{ end; }
-	script	Croix camp#bat	1_FLAG_EAGLE,{ end; }

bat_b03,304,231,1	duplicate(Guillaume camp#bat)	Guillaume camp#td_1	1_FLAG_LION
bat_b03,319,231,1	duplicate(Guillaume camp#bat)	Guillaume camp#td_2	1_FLAG_LION
bat_b03,304,218,1	duplicate(Guillaume camp#bat)	Guillaume camp#td_3	1_FLAG_LION
bat_b03,319,218,1	duplicate(Guillaume camp#bat)	Guillaume camp#td_4	1_FLAG_LION
bat_b03,304,231,1	duplicate(Guillaume camp#bat)	Guillaume camp#td_5	1_FLAG_LION
bat_b03,304,231,1	duplicate(Guillaume camp#bat)	Guillaume camp#td_6	1_FLAG_LION
bat_b03,335,142,1	duplicate(Guillaume camp#bat)	Guillaume camp#td_7	1_FLAG_LION
bat_b03,335,157,1	duplicate(Guillaume camp#bat)	Guillaume camp#td_8	1_FLAG_LION
bat_b03,390,16,1	duplicate(Guillaume camp#bat)	Guillaume camp#td_9	1_FLAG_LION
bat_b03,292,163,1	duplicate(Guillaume camp#bat)	Guillaume camp#td_10	1_FLAG_LION
bat_b03,292,136,1	duplicate(Guillaume camp#bat)	Guillaume camp#td_11	1_FLAG_LION
bat_b03,241,185,1	duplicate(Guillaume camp#bat)	Guillaume camp#td_12	1_FLAG_LION
bat_b03,247,179,1	duplicate(Guillaume camp#bat)	Guillaume camp#td_13	1_FLAG_LION

bat_b03,96,81,1	duplicate(Croix camp#bat)	Croix camp#td_1	1_FLAG_EAGLE
bat_b03,96,68,1	duplicate(Croix camp#bat)	Croix camp#td_2	1_FLAG_EAGLE
bat_b03,79,81,1	duplicate(Croix camp#bat)	Croix camp#td_3	1_FLAG_EAGLE
bat_b03,79,68,1	duplicate(Croix camp#bat)	Croix camp#td_4	1_FLAG_EAGLE
bat_b03,96,81,1	duplicate(Croix camp#bat)	Croix camp#td_5	1_FLAG_EAGLE
bat_b03,96,81,1	duplicate(Croix camp#bat)	Croix camp#td_6	1_FLAG_EAGLE
bat_b03,59,164,1	duplicate(Croix camp#bat)	Croix camp#td_7	1_FLAG_EAGLE
bat_b03,59,137,1	duplicate(Croix camp#bat)	Croix camp#td_8	1_FLAG_EAGLE
bat_b03,10,296,1	duplicate(Croix camp#bat)	Croix camp#td_9	1_FLAG_EAGLE
bat_b03,110,162,1	duplicate(Croix camp#bat)	Croix camp#td_10	1_FLAG_EAGLE
bat_b03,110,137,1	duplicate(Croix camp#bat)	Croix camp#td_11	1_FLAG_EAGLE
bat_b03,152,120,1	duplicate(Croix camp#bat)	Croix camp#td_12	1_FLAG_EAGLE
bat_b03,158,114,1	duplicate(Croix camp#bat)	Croix camp#td_13	1_FLAG_EAGLE

// MapFlags
// *********************************************************************

bat_b03	mapflag	battleground	2
bat_b03	mapflag	nomemo
bat_b03	mapflag	nosave	SavePoint
bat_b03	mapflag	noteleport
bat_b03	mapflag	nowarp
bat_b03	mapflag	nowarpto
bat_b03	mapflag	noreturn
bat_b03	mapflag	nobranch
bat_b03	mapflag	nopenalty
